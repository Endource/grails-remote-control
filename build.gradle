buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:hibernate4:5.0.2"
    }
}

group "io.github.endource"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"maven-publish"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-gsp"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = "2.9"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    provided "org.springframework.boot:spring-boot-starter-logging"
    provided "org.springframework.boot:spring-boot-autoconfigure"
    provided "org.grails:grails-core"
    provided "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-starter-tomcat"
    provided "org.grails:grails-dependencies"
    provided "org.grails:grails-web-boot"
    provided "org.grails.plugins:cache"
    provided "org.grails.plugins:scaffolding"
    compile "io.remotecontrol:remote-core:0.7"
    compile "io.remotecontrol:remote-transport-http:0.7", {
        exclude module: "servlet-api"
    }

    provided 'org.grails.plugins:hibernate4'
    provided 'org.hibernate:hibernate-ehcache'
    provided "com.h2database:h2"

    console "org.grails:grails-console"
    profile "org.grails.profiles:web-plugin:3.1.4"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

jar {
    exclude "test/**"
    exclude "test/**/**"
}

sourcesJar {
    exclude "test/**"
    exclude "test/**/**"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/endource/grails-remote-control"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId 'remote-control'
            version project.version

            from components.java
            artifact sourcesJar

            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.groupId.text().contains("org.grails") ||
                            it.groupId.text().contains("org.hibernate") ||
                            it.groupId.text().contains("org.springframework") ||
                            it.groupId.text().contains("com.h2database")
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}
